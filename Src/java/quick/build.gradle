configurations {
    xjc
}

dependencies {
    xjc group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.2.11'
    xjc group: 'com.sun.xml.bind', name: 'jaxb-xjc', version: '2.2.11'
    xjc group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.2.11'
    xjc group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-basics-ant', version: '0.9.4'
    xjc group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-basics', version: '0.9.4'
    xjc group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-fluent-api', version: '3.0'
    compile group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-basics', version: '0.9.4'
}

ext.xjc = [
    schema:  "${projectDir}/schema/quick.xsd",
    binding:  "${projectDir}/schema/quick-binding.xjb",
    destDir: 'src/generated/java'
]

task generateSources << {
    System.setProperty('javax.xml.accessExternalSchema', 'file')
    file(xjc.destDir).mkdirs()

    ant.taskdef(name: 'xjc', classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task', classpath: configurations.xjc.asPath)
    ant.xjc(destdir: xjc.destDir, schema: xjc.schema, binding: xjc.binding) {
        arg(line: '-disableXmlSecurity -Xfluent-api -Xequals -XhashCode -XtoString')
    }
}

compileJava.dependsOn generateSources
sourceSets.main.java.srcDirs += xjc.destDir

clean {
    delete xjc.destDir
}